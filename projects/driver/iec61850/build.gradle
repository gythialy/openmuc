def projectName = "OpenMUC Driver - IEC 61850"
def projectDescription = "IEC 61850 driver for the OpenMUC framework."

configurations.create('embed')

def openiec61850version = '1.7.0'
def josiVersion = '1.3.3'
def jasn1name = 'jasn1-iec61850mod'
def jasn1version = '1.10.0'

dependencies {
    implementation project(':openmuc-core-spi')
    implementation group: 'org.openmuc', name: 'openiec61850', version: openiec61850version
    testImplementation 'org.mockito:mockito-core:2.7.22'

    embed group: 'org.openmuc', name: 'openiec61850', version: openiec61850version
    embed group: 'org.openmuc', name: 'josistack', version: josiVersion
    embed group: 'org.openmuc', name: 'jositransport', version: josiVersion
    embed group: 'org.openmuc', name: jasn1name, version: jasn1version
}

jar {
    manifest {
        name = projectName
        instruction 'Bundle-ClassPath', '.,lib/openiec61850-' + openiec61850version + '.jar,lib/josistack-' + josiVersion + '.jar,lib/jositransport-' + josiVersion + '.jar,lib/' + jasn1name + '-' + jasn1version + '.jar'
        instruction 'Import-Package', '!org.openmuc.openiec61850.*,javax.net,*' + jarDefaultImportPackageVersion
        instruction 'Export-Package', ''
    }
}

jar {
    into('lib') {
        from configurations.embed
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name projectName
                description projectDescription
            }
        }
    }
}
